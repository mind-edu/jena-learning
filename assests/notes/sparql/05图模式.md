# SPARQL 1.1 Query Language

W3C Recommendation 21 March 2013

[原文地址](https://www.w3.org/TR/sparql11-query/)

## 5. 图模式(Graph Pattern)

SPARQL是基于图模式匹配的，更多复杂的图模式可以通过结合更小的模式来组合而成。

* 基础图模式(Basis Graph Pattern):必须匹配给出所有三元组
* 组图模式(Group Graph Pattern):必须匹配给出所有图模式
* 可选图模式(Optional Graph Pattern):可以通过附加的模式来扩展匹配结果
* 可替代图模式(Alternative Graph Pattern):尝试至少两个可能的模式进行匹配
* 命名图模式(Patterns on Named Graphs):在非命名图中匹配模式

### 5.1 基础图模式

基础的图模式的组成有两部分：

* 一系列的三元组模式
* 附加的filter

SPARQL通过子图匹配来计算基本图模式，子图匹配的机制使用的预定义好的，[定义的文档](http://www.w3.org/TR/sparql11-entailment/)，匹配机制可以被扩展。

### 5.2 组图模式

在一个SPARQL语句查询中，一组图模式使用`{}`进行包裹，例如

```
PREFIX foaf:  <http://xmlns.com/foaf/0.1/>
SELECT ?name ?mbox
WHERE {
    {
        ?x foaf:name ?name .
    }
    {
        ?x foaf:mbox ?mbox .
    }
}
```

示例代码中，就有一组图模式，由两个基础的图模式组成，两个基础的图模式中各有一个三元组模式。

在前几章中，我们的查询语句都是

```
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
SELECT ?name ?mbox
WHERE  {
          ?x foaf:name ?name .
          ?x foaf:mbox ?mbox .
       }
```

实际上查询的模式也是组图模式，只是这一组图模式中只包含了一个基本的图模式。

#### 5.2.1 空组模式

一个空的组模式，其实就是

```
{}
```

#### 5.2.2 Filter作用范围

一个FILTER的作用范围为该FILTER出现的组图模式，即出现的那个`{}`。

例如，以下三种写法效果都是一样的。

```
// 写法1
{  ?x foaf:name ?name .
    ?x foaf:mbox ?mbox .
    FILTER regex(?name, "Smith")
 }

 // 写法2
 {  FILTER regex(?name, "Smith")
    ?x foaf:name ?name .
    ?x foaf:mbox ?mbox .
 }

 // 写法3
 {  ?x foaf:name ?name .
    FILTER regex(?name, "Smith")
    ?x foaf:mbox ?mbox .
 }
```

### 5.3 可选模式匹配

在SPARQL查询中，可以通过`OPTIONAL`来添加可选的模式匹配，模式匹配在至少满足基本图模式的情况下，执行可选的模式匹配不会缩减满足基本图模式的结果集。

举个例子

```
@prefix foaf:       <http://xmlns.com/foaf/0.1/> .
@prefix rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .

_:a  rdf:type        foaf:Person .
_:a  foaf:name       "Alice" .
_:a  foaf:mbox       <mailto:alice@example.com> .
_:a  foaf:mbox       <mailto:alice@work.example> .

_:b  rdf:type        foaf:Person .
_:b  foaf:name       "Bob" .
```

在上面的数据中，我们查询拥有属性`foaf:name`的基础图模式，添加可选模式，如果在结果集还存在`foaf:mbox`属性的图模式，将其查找出来。

```
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT ?name ?mbox
WHERE {
    ?x foaf:name ?name .
    OPTIONAL {
        ?x foaf:mbox ?mbox
    }
}
```

![sparql-example-13.png](../../images/sparql-example-13.png)

从查询结果可以看到，因为`Bob`是没有`foaf:mbox`，因此`mbox`的值为空，但是并没有从结果集中被删除掉。

#### 5.3.1 可选模式中使用Filter

在可选的模式中，也可以使用`FILTER`，举个就明白了。

```
@prefix dc:   <http://purl.org/dc/elements/1.1/> .
@prefix :     <http://example.org/book/> .
@prefix ns:   <http://example.org/ns#> .

:book1  dc:title  "SPARQL Tutorial" .
:book1  ns:price  42 .
:book2  dc:title  "The Semantic Web" .
:book2  ns:price  23 .
```

```
PREFIX  dc:  <http://purl.org/dc/elements/1.1/>
PREFIX  ns:  <http://example.org/ns#>
SELECT ?title ?price
WHERE {
    ?x dc:title ?title .
    OPTIONAL {
        ?x ns:price ?price .
        FILTER (?price < 30)
    }
}
```

![sparql-example-14.png](../../images/sparql-example-14.png)

#### 5.3.2 多重可选模式

在SPARQL查询语句中，可以声明多个`OPTIONAL`。举个例子

```
@prefix foaf:       <http://xmlns.com/foaf/0.1/> .

_:a  foaf:name       "Alice" .
_:a  foaf:homepage   <http://work.example.org/alice/> .

_:b  foaf:name       "Bob" .
_:b  foaf:mbox       <mailto:bob@work.example> .
```

```
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT ?name ?mbox ?hpage
WHERE {
    ?x foaf:name ?name .
    OPTIONAL {
        ?x foaf:mbox ?mbox .
    }
    OPTIONAL {
        ?x foaf:homepage ?hpage .
    }
}
```

![sparql-example-15.png](../../images/sparql-example-15.png)

### 5.4 可替代模式匹配

SPARQL提供了一种组合多个图模式的方式，只要满足多个图模式的其中之一即可组成结果集。

使用`UNION`来完成这种匹配模式，下面举个例子。

```
@prefix dc10:  <http://purl.org/dc/elements/1.0/> .
@prefix dc11:  <http://purl.org/dc/elements/1.1/> .

_:a  dc10:title     "SPARQL Query Language Tutorial" .
_:a  dc10:creator   "Alice" .

_:b  dc11:title     "SPARQL Protocol Tutorial" .
_:b  dc11:creator   "Bob" .

_:c  dc10:title     "SPARQL" .
_:c  dc11:title     "SPARQL (updated)" .
```

我们匹配拥有属性`dc10:title`或`dc11:title`的三元组，查询语句为：

```
PREFIX dc10:  <http://purl.org/dc/elements/1.0/>
PREFIX dc11:  <http://purl.org/dc/elements/1.1/>

SELECT ?title
WHERE {
    {
        ?book dc10:title ?title .
    } UNION {
        ?book dc11:title ?title .
    }
}
```

查询结果为:

![sparql-example-16.png](../../images/sparql-example-16.png)




